
*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends

.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

C1 in n_inv 220n


X1 n_inv inv vcc vee out_amp uA741

R1 n_inv 0 1k
R4 inv 0 1k
R3 out_amp inv 110k
R2 out_amp out 1k

* load
C2 out 0 110n



.op
.end

.control

*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0 

print all

* time analysis
tran 1e-5 1e-2

hardcopy vout.ps v(out)


* frequency analysis
ac dec 10 10 100MEG

hardcopy vdbf.ps vdb(out)

let phasef(out) = 180/PI*ph(v(out))

hardcopy phasef.ps phasef(out)


*Gain, frequency
let gain = vdb(out)-vdb(in)
meas ac gainmaxdb MAX gain
let gainmax = 10^(gainmaxdb/20)


let gaindbm3 = gainmaxdb - 3

meas ac lcf WHEN gain=gaindbm3
meas ac hcf WHEN gain=gaindbm3 CROSS=LAST

let freq = sqrt(lcf*hcf)

let freqdev = abs(freq - 1000)

let bandwidth=hcf-lcf


meas ac gaindb FIND vdb(out) AT=1000
let lingain = 10^(gaindb/20)

let gaindev = abs(lingain-100)

*merit

let cost = 13323.2904+113+0.110*3
let merit = 1/(cost*(freqdev*gaindev+1e-6))

*Input impendance
let Zin = abs(-v(in)[20]/i(Vin)[20])


echo "gain_TAB"
echo "Gain = $&gainmax "
echo "Gain dB = $&gainmaxdb "
echo "Central Frequency = $&freq "
echo "Linear Gain Deviation = $&gaindev "
echo "Frequency Deviation = $&freqdev "
echo "Bandwidth = $&bandwidth "
echo "gain_END"

echo "merito_TAB"
echo "Cost = $&cost "
echo "Merit = $&merit "
echo "merito_END"

echo "zin_TAB"
echo "Input Impedance = $&Zin "
echo "zin_END"

.endc 
