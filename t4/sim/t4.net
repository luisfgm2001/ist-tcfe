.options savecurrents

.param Rin_val=0.1k
.param R1_val=90k
.param R2_val=20k
.param Rc_val=0.86k
.param Re_val=0.2k
.param Rout_val=0.052k
.param Ci_val=102u
.param Cb_val=2185u
.param Co_val=1466u

.csparam Rin_val={Rin_val}
.csparam R1_val={R1_val}
.csparam R2_val={R2_val}
.csparam Rc_val={Rc_val}
.csparam Re_val={Re_val}
.csparam Rout_val={Rout_val}
.csparam Ci_val={Ci_val}
.csparam Cb_val={Cb_val}
.csparam Co_val={Co_val}

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)


Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 {Rin_val}
Ci in2 base {Ci_val}

* bias circuit
Vcc vcc 0 12.0
R1 vcc base {R1_val}
R2 base 0 {R2_val}

* gain stage
Q1 coll base emit BC547A
Rc vcc coll {Rc_val}
Re emit 0 {Re_val}
Cb emit 0 {Cb_val}


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc {Rout_val}
Co emit2 out {Co_val}

* load
RL out 0 8


.end

.control

set hcopypscolor=1
set color0=white
set color1=black
set color2=pink
set color3=blue
set color4=violet
set color5=rgb:255/153/204
set color6=rgb:255/153/204

*OP analysis
op

let vce1=v(coll)-v(emit)
let vbe1=v(base)-v(emit)

let vec2=v(emit2)
let veb2=v(emit2)-v(coll)


echo "op_TAB"
print all
echo "op_END"

* time analysis
tran 1e-5 1e-2

hardcopy vo1.ps vdb(coll)
echo vo1_FIG

hardcopy vout.ps v(out)
echo vout_FIG

* frequency analysis
ac dec 10 10 100MEG

hardcopy vo1f.ps vdb(coll)
echo vo1f_FIG

hardcopy vo2f.ps vdb(out)
echo vo2f_FIG

let g = v(out)[40]/v(in)[40]
let gain = abs(g)
let gainDB = 20*log10(abs(gain))


*Determining lowcutoff and highcutoff frequencies 
let max_min_3 = vecmax(vdb(out))-3

meas ac lowf WHEN vdb(out) = max_min_3 RISE = 1
meas ac uppf WHEN vdb(out) = max_min_3 CROSS = LAST

let bandwidth = uppf-lowf

*input impedance in ohm
let Z_in1 = v(in2)[40]/vin#branch[40]/(-1)

*Cost and Merit
let cost = (Rin_val + R1_val + R2_val + Re_val + Rc_val + Rout_val)/1000 + (Ci_val + Cb_val + Co_val)*10^6 + 0.1*2
let merit = gain*bandwidth/(cost*lowf)

let Zin = abs(Z_in1)

echo  "data_TAB"
print Gain GainDB lowf uppf bandwidth Zin
echo  "data_END"


quit
.endc
